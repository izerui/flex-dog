<?xml version="1.0"?>
<mx:TextInput xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx"
              change="changeHandler(event)">

    <fx:Metadata>
        [Event(name="textChange", type="com.github.izerui.file.event.TextChangeEvent")]
    </fx:Metadata>

    <fx:Script>
        <![CDATA[
        import com.github.izerui.file.event.TextChangeEvent;

        import mx.controls.Button;

        [Bindable]
        [Embed(source="/assets/img/close.png")]
        private var imgSource:Class;

        private var clearButton:Button;


        private function changeHandler(event:Event):void {
            if (this.text && this.getChildByName("closeButton") == null) {
                if (!clearButton) {
                    clearButton = new Button;
                    clearButton.name = "closeButton";
                    clearButton.setStyle("icon", imgSource);
                    clearButton.height = 16;
                    clearButton.width = 16;
                    clearButton.useHandCursor = true;
                    clearButton.addEventListener(MouseEvent.CLICK, clearButtonClick);
                }
                this.addChild(clearButton);
                clearButton.move(this.width - clearButton.width - (this.height - clearButton.height) / 2, (this.height - clearButton.height) / 2);
            } else if (!this.text && this.getChildByName("closeButton")) {
                this.removeChild(clearButton);
            }

            var textChangeEvent:TextChangeEvent = new TextChangeEvent("textChange");
            textChangeEvent.text = this.text;
            this.dispatchEvent(textChangeEvent);
        }

        protected function clearButtonClick(event:MouseEvent):void {
            reset();
            dispatchEvent(new Event(Event.CHANGE));
        }

        public function reset():void {
            text = null;
            if (this.getChildByName("closeButton")) {
                this.removeChild(clearButton);
            }
            this.setFocus();
        }
        ]]>
    </fx:Script>
</mx:TextInput>
