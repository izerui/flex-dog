<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:mx="library://ns.adobe.com/flex/mx"
         xmlns:components="com.github.izerui.file.components.*"
         creationComplete="loadServers()">

    <fx:Script>
		<![CDATA[
        import com.github.izerui.file.controls.titlewin.AlertWindow;
        import com.github.izerui.file.controls.titlewin.DetailTitleWindow;
        import com.github.izerui.file.controls.titlewin.NewService;
        import com.github.izerui.file.controls.uploader.ServletUploader;
        import com.github.izerui.file.event.FileUploadEvent;
        import com.github.izerui.file.event.I18nEvent;
        import com.github.izerui.file.utils.RemoteObjectUtils;
        import com.github.izerui.file.vo.FileItem;

        import mx.collections.ArrayCollection;
        import mx.controls.Alert;
        import mx.core.Application;
        import mx.core.FlexGlobals;
        import mx.events.ItemClickEvent;
        import mx.events.ListEvent;
        import mx.managers.PopUpManager;
        import mx.rpc.events.ResultEvent;
        import mx.utils.ObjectUtil;

        [Bindable]
        private var dataList:ArrayCollection;

        [Embed("/assets/img/Server.png")]
        private var serverImg:Class;

        public function loadFiles():void {

            var server:String = "";
            if (serverList.selectedIndex != -1) {
                server = serverList.selectedItem.Name;
            }
            var selFileId:String = null;
            if (myFileGrid.selectedIndex != -1) {
                selFileId = myFileGrid.selectedItem.id;
            }
            RemoteObjectUtils.execute("fileService", "listFiles", function (event:Object) {
                dataList = ArrayCollection(event.result);
                filterResult();
                if (selFileId) {
                    for (var i = 0; i < dataList.length; i++) {
                        var item:FileItem = dataList[i];
                        if (item.id == selFileId) {
                            myFileGrid.selectedIndex = i;
                            myFileGrid.scrollToIndex(i);
                            break;
                        }
                    }
                }
            }, server);

        }


        protected function filterResult():void {
            if (!searchKey.text) {
                myFileGrid.dataProvider = dataList;
                return;
            }
            var tempSearchList:ArrayCollection = new ArrayCollection;

            for each (var fi:FileItem in dataList) {
                if (fi.fileName.indexOf(searchKey.text) != -1) {
                    tempSearchList.addItem(ObjectUtil.clone(fi));
                }
            }
            myFileGrid.dataProvider = tempSearchList;
        }

        private function loadServers():void {
            RemoteObjectUtils.execute("ucloudService", "getDescribeUHostInstance", function (event:ResultEvent) {
                var json:String = String(event.result);
                var data:Object = JSON.parse(json);
                serverList.dataProvider = data.UHostSet;
                ArrayCollection(serverList.dataProvider).addItem({
                    Name: "全部"
                });
                if (serverList.dataProvider && serverList.dataProvider.length > 0) {
                    serverList.selectedIndex = serverList.dataProvider.length - 1;
                }
                loadFiles();
            })
        }

        private function i18nResourceChange(event:I18nEvent):void {
            (linkButtonBar.dataProvider as ArrayCollection).refresh();
        }

        [Bindable]
        [Embed(source="/assets/img/bar_img/back.png")]
        private var button1_class:Class;

        protected function linkButtonBar_itemClickHandler(event:ItemClickEvent):void {
            switch (event.item["itemData"]) {
                case 100: {
//                    RemoteObjectUtils.execute("fileService", "init", function (event:Object) {
//                        loadServers();
//                    });
                    loadFiles();
                    return;
                }
                case 1: {
                    newService();
                    return;
                }
                case 4: {
                    RemoteObjectUtils.execute("fileService", "listFiles", function (event:Object) {
                        var fileList:ArrayCollection = ArrayCollection(event.result);

                        var servletUploader:ServletUploader = new ServletUploader;
                        servletUploader.fileList = fileList;
                        PopUpManager.addPopUp(servletUploader, FlexGlobals.topLevelApplication as DisplayObjectContainer, true);
                        PopUpManager.centerPopUp(servletUploader);
                        servletUploader.addEventListener(FileUploadEvent.UPLOAD_COMPLETE, fileUploadComplete);
                        servletUploader.setFocus();
                    }, "");
                    break;
                }
                case 7:
                    if (myFileGrid.selectedIndex != -1) {
                        if (!myFileGrid.selectedItem.filePath) {
                            Alert.show("请先上传程序文件,再发布.", "提示");
                            return;
                        }
                        var deployWin:AlertWindow = new AlertWindow();
                        deployWin.addEventListener("successEvent", exec);
                        PopUpManager.addPopUp(deployWin, FlexGlobals.topLevelApplication as DisplayObjectContainer, true);
                        PopUpManager.centerPopUp(deployWin);

                    }
                    break;
                case 8:
                    if (myFileGrid.selectedIndex != -1) {
                        navigateToURL(new URLRequest("download?id=" + myFileGrid.selectedItem["id"]), "_blank");
                    }
                    break;
                case 10:
                    if (myFileGrid.selectedIndex != -1 && myFileGrid.selectedItem.appId && myFileGrid.selectedItem.instanceId) {
                        var deployWin:AlertWindow = new AlertWindow();
                        deployWin.addEventListener("successEvent", stopService);
                        PopUpManager.addPopUp(deployWin, FlexGlobals.topLevelApplication as DisplayObjectContainer, true);
                        PopUpManager.centerPopUp(deployWin);
                    }
                    break;
                case 9:
                    if (myFileGrid.selectedIndex != -1 && myFileGrid.selectedItem.appId && myFileGrid.selectedItem.instanceId) {
                        var deployWin:AlertWindow = new AlertWindow();
                        deployWin.addEventListener("successEvent", startService);
                        PopUpManager.addPopUp(deployWin, FlexGlobals.topLevelApplication as DisplayObjectContainer, true);
                        PopUpManager.centerPopUp(deployWin);
                    }
                    break;
            }

        }

        protected function exec(event:Event):void {

            RemoteObjectUtils.execute("fileService", "exec", execResult, myFileGrid.selectedItem.id);

            function execResult(event:ResultEvent):void {
                var output = String(event.result);
                myFileGrid.selectedItem["deployTimeStr"] = "1秒前";
                ArrayCollection(myFileGrid.dataProvider).itemUpdated(myFileGrid.selectedItem, "relativeDeployTime");

                var popUp:DetailTitleWindow = new DetailTitleWindow();
                popUp.output = output ? output : "未知";
                popUp.setFocus();
                PopUpManager.addPopUp(popUp, FlexGlobals.topLevelApplication as DisplayObjectContainer, true)
                PopUpManager.centerPopUp(popUp)
            }
        }

        protected function startService(event:Event):void {
            RemoteObjectUtils.execute("fileService", "startService", null, myFileGrid.selectedItem.appId, myFileGrid.selectedItem.instanceId);
        }

        protected function stopService(event:Event):void {
            RemoteObjectUtils.execute("fileService", "stopService", null, myFileGrid.selectedItem.appId, myFileGrid.selectedItem.instanceId);
        }


        private function newService():void {
            var ns:NewService = new NewService();
            ns.serverDataList = ArrayCollection(serverList.dataProvider);
            ns.addEventListener("addEvent", function (event:Event):void {
                loadFiles();
            });
            ns.setFocus();

            PopUpManager.addPopUp(ns, Application.application as DisplayObject, true);
            PopUpManager.centerPopUp(ns);
        }


        protected function fileUploadComplete(event:FileUploadEvent):void {
            trace("上传完毕");
            loadFiles();
        }


        protected function _icon(item:Object):Class {
            return serverImg;
        }

        private function serverList_itemClickHandler(event:ListEvent):void {
            loadFiles();
        }

        private function view():void {
            if (myFileGrid.selectedIndex == -1) {
                return;
            }
            var popUp:DetailTitleWindow = new DetailTitleWindow();
            popUp.output = JSON.stringify(myFileGrid.selectedItem, null, "  ");
            popUp.setFocus();
            PopUpManager.addPopUp(popUp, FlexGlobals.topLevelApplication as DisplayObjectContainer, true)
            PopUpManager.centerPopUp(popUp)
        }
        ]]>
	</fx:Script>

    <fx:Declarations>
        <!-- 将非可视元素（例如服务、值对象）放在此处 -->
    </fx:Declarations>
    <mx:HDividedBox width="100%" height="100%">
        <mx:TileList id="serverList"
                     columnWidth="50"
                     itemClick="serverList_itemClickHandler(event)"
                     height="100%" width="160" fontSize="12" fontWeight="bold" labelField="Name" iconFunction="_icon">

        </mx:TileList>
        <mx:Box width="100%" height="100%">
            <mx:HBox width="100%">
                <mx:LinkBar id="linkButtonBar" height="100%" iconField="icon"
                            itemClick="linkButtonBar_itemClickHandler(event)">
                    <mx:dataProvider>
                        <s:ArrayCollection>
                            <fx:Object label="新建" itemData="1" icon="@Embed('/assets/img/add.png')"/>
                            <fx:Object label="上传" itemData="4" icon="@Embed('/assets/img/filebrowser.gif')"/>
                            <fx:Object label="上线" itemData="9" icon="@Embed('/assets/img/enable.png')"/>
                            <fx:Object label="下线" itemData="10" icon="@Embed('/assets/img/disable.png')"/>
                            <fx:Object label="发布" itemData="7" icon="@Embed('/assets/img/exec.png')"/>
                            <fx:Object label="刷新" itemData="100" icon="@Embed('/assets/img/qingkong.png')"/>
                        </s:ArrayCollection>
                    </mx:dataProvider>
                </mx:LinkBar>
                <components:SearchInput id="searchKey" width="200" textChange="filterResult()"/>
            </mx:HBox>
            <components:FileDataGrid id="myFileGrid" width="100%" height="100%" itemDoubleClick="view()"
                                     doubleClickEnabled="true">
            </components:FileDataGrid>
        </mx:Box>
    </mx:HDividedBox>
</mx:VBox>
